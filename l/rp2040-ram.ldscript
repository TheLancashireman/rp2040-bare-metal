/*  rp2040-ram.ldscript - linker script for loading application directly to RAM via UF2 loader
 *
 *  (c) David Haworth
 *
 *  This file is part of rp2040-bare-metal.
 *
 *  rp2040-bare-metal is free software: you can redistribute it and/or modify
 *  it under the terms of the GNU General Public License as published by
 *  the Free Software Foundation, either version 3 of the License, or
 *  (at your option) any later version.
 *
 *  rp2040-bare-metal is distributed in the hope that it will be useful,
 *  but WITHOUT ANY WARRANTY; without even the implied warranty of
 *  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
 *  GNU General Public License for more details.
 *
 *  You should have received a copy of the GNU General Public License
 *  along with rp2040-bare-metal.  If not, see <http://www.gnu.org/licenses/>.
*/


/*  The RP2040 has 256 KiB RAM located at 0x20000000. The RAM is arranged in four banks, and the
 *	image at this address is "striped" (i.e. address xx0 is in bank 0, address xx4 is in bank 1,
 *	address xx8 is in bank 2 and address xxc is in bank 3.
 *
 *	This script assumes that the UF2 boot loader loads the program directly into RAM.
 *	This means we need an entry point at the start that initializes the stack pointer.
*/

MEMORY
{
	ram		(rw)	: org = 0x20000000, len = 0x40000	/* 256 KiB */
}

SECTIONS
{
    .text	:
	{
		o/rp2040-boot.o(.text*)
		*(.text*)
	} > ram

	.rodata	BLOCK(256) :
	{
		o/rp2040-vectors.o(.rodata*)
		*(.rodata*)
	} > ram

	.data	BLOCK(256) :
	{
		rp2040_start_data = .;
		*(.data*)
		FILL(0x00)
		. = ALIGN(16);
		rp2040_end_data = .;
	} > ram

    .bss	BLOCK(16) (NOLOAD) :
	{
		rp2040_start_bss = .;
		*(.bss*)
		rp2040_end_bss = .;
	} > ram

	/* Main stack at the top of memory, 2 KiB.
	*/
	rp2040_stacktop = (0x20000000 + 0x40000);

	/* These sections appear to be generated by the compiler.
	 * We doesn't use them.
	*/
    .stack              : { *(.stack)       }

	/* The remainder are DWARF-2 debug sections. They contain
	 * ELF relocations and must be located at zero.
	*/
    . = 0x0;
    .debug_aranges      : { *(.debug_aranges)   }
    . = 0x0;
    .debug_pubnames     : { *(.debug_pubnames)  }
    . = 0x0;
    .debug_info         : { *(.debug_info)  }
    . = 0x0;
    .debug_abbrev       : { *(.debug_abbrev)    }
    . = 0x0;
    .debug_line         : { *(.debug_line)  }
}
